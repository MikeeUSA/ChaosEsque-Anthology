#ifdef REGISTER_WEAPON
REGISTER_WEAPON(LIGHTPISTOL, w_lightpistol, IT_NAILS, 2, WEP_FLAG_NORMAL | WEP_FLAG_RELOADABLE | WEP_TYPE_HITSCAN, BOT_PICKUP_RATING_LOW, "lightpistol", "lightpistol", _("9mm Pistol"))
#else
#ifdef SVQC

.float lightpistol_release;
.float lightpistol_jammed;
.float lightpistol_unjam;
.float lightpistol_unjam_time;
.float lightpistol_click;
.float lightpistol_suppressed;
.float lightpistol_animempty;
.float lightpistol_animset;
.float lightpistol_nextanim;
.float lightpistol_nextbot;

void W_LightPistol_Attack (void)
{
	float	ammoamount;
	float	d;
	float	f;
	float	spread;
	float	bulletspeed;
	float	bulletconstant;
	entity flash;

	ammoamount = autocvar_g_balance_lightpistol_primary_ammo;
	d = autocvar_g_balance_lightpistol_primary_damage;
	f = autocvar_g_balance_lightpistol_primary_force;
	spread = autocvar_g_balance_lightpistol_primary_spread;
	bulletspeed = autocvar_g_balance_lightpistol_primary_speed;
	bulletconstant = autocvar_g_balance_lightpistol_primary_bulletconstant;

	W_DecreaseAmmo(ammo_nails, ammoamount, autocvar_g_balance_lightpistol_reload_ammo);
	
	if (autocvar_g_balance_lightpistol_jamrate &&
 	(autocvar_g_balance_lightpistol_jamrate > random() ))
 	{
		if ((clienttype(self) == CLIENTTYPE_REAL)) {
			sprint(self, "Your pistol jammed\n");
		}
		self.lightpistol_jammed = 1;
	} else { //Start Not Jammed

	if not(self.lightpistol_suppressed) {
		W_SetupShot (self, autocvar_g_antilag_bullets && bulletspeed >= autocvar_g_antilag_bullets, 5, "weapons/ffsl_wppq_fire.wav", CH_WEAPON_A, d);
	} else {
		W_SetupShot (self, autocvar_g_antilag_bullets && bulletspeed >= autocvar_g_antilag_bullets, 5, "weapons/uzi_fire_doublesuppressed.wav", CH_WEAPON_A, d);
		spamsound (self, CH_SHOTS, "weapons/dryfire.wav", VOL_BASE * 0.23, ATTN_NORM);
	}

	W_MaybeShootFromEyeForAltAim(self); //shoot from the eye maybe if in alt aim so that shooting through windows works correctly when aiming
	
	fireBallisticBullet(w_shotorg, w_shotdir, spread, bulletspeed, 5, d, autocvar_g_balance_lightpistol_primary_headshotaddeddamage, f, WEP_LIGHTPISTOL, PROJECTILE_BULLET9MM, 1, bulletconstant, autocvar_g_balance_lightpistol_primary_coreshotaddeddamage);
	endFireBallisticBullet();

	if not(self.lightpistol_suppressed) {	
		pointparticles(particleeffectnum("uzi_muzzleflash"), w_shotorg, w_shotdir * 1000, autocvar_g_balance_lightpistol_primary_ammo);
	
		PlayerGunRise(self, autocvar_g_balance_lightpistol_gunrise, random() * autocvar_g_balance_lightpistol_gunrun);

		// muzzle flash for 1st person view
		flash = spawn();
		setmodel(flash, "models/uziflash.md3"); // precision set below
		flash.think = SUB_Remove;
		flash.nextthink = time + 0.06;
		flash.effects = EF_ADDITIVE | EF_FULLBRIGHT | EF_LOWPRECISION;
		W_AttachToShotorg(flash, '-0.75 0 0');
	} else {
		PlayerGunRise(self, autocvar_g_balance_lightpistol_suppressed_gunrise, random() * autocvar_g_balance_lightpistol_suppressed_gunrun);	
	}
	
	if (autocvar_g_casings >= 2)
		SpawnCasing (((random () * 50 + 50) * v_right) - (v_forward * (random () * 25 + 25)) - ((random () * -100 - 120) * v_up), 2, vectoangles(v_forward),'0 250 0', 100, 5, self);

	
	} //End Not Jammed
}


float W_LightPistol_NeedManualReload() {
	if  (autocvar_g_manualreload && (autocvar_g_balance_lightpistol_reload_ammo && self.clip_load < autocvar_g_balance_lightpistol_primary_ammo))
	{
		if (!self.lightpistol_click && (self.BUTTON_ATCK)) {
			spamsound (self, CH_SHOTS, "weapons/dryfire.wav", VOL_BASE * 0.25, ATTN_NORM);
			self.lightpistol_click = 1;
		}
		else if not(self.BUTTON_ATCK)
		{
			self.lightpistol_click = 0;
		}
		return 1;
	} else {
		return 0;
	}
	
}


void spawnfunc_weapon_lightpistol (void)
{
	weapon_defaultspawnfunc(WEP_LIGHTPISTOL);
}


.float lightpistol_primarytime;

float w_lightpistol(float req)
{
	float ammo_amount;
	if (req == WR_AIM) {
		if (W_LightPistol_NeedManualReload()) { weapon_action(self.weapon, WR_RELOAD); } // bot reload if automatic reload off

		if (self.lightpistol_jammed) {
			 weapon_action(self.weapon, WR_RELOAD);
		}
		
		self.BUTTON_ATCK=FALSE;
		self.BUTTON_ATCK2=FALSE;

		if (self.lightpistol_release) {
			if(autocvar_g_antilag_bullets)
				self.BUTTON_ATCK = bot_aim(1000000, 0, 0.001, FALSE);
			else
				self.BUTTON_ATCK = bot_aim(autocvar_g_balance_lightpistol_primary_speed, 0, 0.001, FALSE);
		}
		
	}
	else if (req == WR_THINK && !W_LightPistol_NeedManualReload())
	{
		if (self.lightpistol_suppressed) {
			if (!autocvar_g_balance_lightpistol_suppressornotincluded
			|| self.inv_misc_suppressor9mm)
			{
				//Nothing
			} else {
				self.lightpistol_suppressed = 0;
				self.lightpistol_animset = 1;
			}
		}
	
	
	
		if(autocvar_g_balance_lightpistol_reload_ammo && self.clip_load < autocvar_g_balance_lightpistol_primary_ammo) // forced reload
		{
			// don't force reload an empty lightpistol if its melee attack is active
			if not(autocvar_g_balance_lightpistol_secondary && self.ammo_nails < autocvar_g_balance_lightpistol_primary_ammo)
				weapon_action(self.weapon, WR_RELOAD);
				//self.lightpistol_release = 0;
		}
		else
		{
		  if (self.lightpistol_jammed && self.lightpistol_unjam)
		  {
		  	if (time >= self.lightpistol_unjam_time) {
				self.lightpistol_jammed = 0;
				self.lightpistol_unjam = 0;
				if ((clienttype(self) == CLIENTTYPE_REAL)) {
					sprint(self, "You cleared the jam\n");
				}
			}
		  } else {
			float bp_skipme;
			if (self.BUTTON_ATCK)
			if (self.lightpistol_release)
			if (self.lightpistol_jammed != 1)
			if (weapon_prepareattack_check(0, 0))
			{
				if(clienttype(self) == CLIENTTYPE_BOT) {
					//Nope
					//print("I bot", ftos(self.lightpistol_nextbot)," \n");
				} else {
					self.lightpistol_nextbot = 0;
				}
				
				if (time >= self.lightpistol_nextbot) {
					W_LightPistol_Attack();
					ATTACK_FINISHED(self) = time + autocvar_g_balance_lightpistol_primary_refire * W_WeaponRateFactor();
					weapon_thinkf(WFRAME_FIRE1, autocvar_g_balance_lightpistol_primary_animtime, w_ready);
					self.lightpistol_release = 0;
					self.lightpistol_animempty = 1;
					self.lightpistol_animset = 1;
					self.lightpistol_nextanim = time + autocvar_g_balance_lightpistol_primary_rfanim;
					bp_skipme = 1;
					//self.lightpistol_nextbot = time + (autocvar_g_balance_lightpistol_primary_refire * 50 * random());
					self.lightpistol_nextbot = time + 0.1 + (0.4 * random());
					
					if (autocvar_g_balance_lightpistol_fullauto == 1) {
						self.lightpistol_release = 1;
						self.lightpistol_nextbot = time;
					}
				}
			}
			
			if not(bp_skipme) {
				if (self.lightpistol_jammed != 1)
				if (time >= self.lightpistol_nextanim)
				if (self.lightpistol_animempty)
				{
					self.lightpistol_animempty = 0;
					self.lightpistol_animset = 1;
				}
			}
			
			if not(self.BUTTON_ATCK) {
				self.lightpistol_release = 1;
			}
				
			if (self.lightpistol_animset)
			if (self.lightpistol_jammed != 1) {
				if (self.lightpistol_suppressed) {
					if (self.lightpistol_animempty)
						self.weaponname = "lightpistol_suppressed_empty";
					else
						self.weaponname = "lightpistol_suppressed";
				} else if (!self.lightpistol_suppressed) {
					if (self.lightpistol_animempty)
						self.weaponname = "lightpistol_empty";
					else
						self.weaponname = "lightpistol";
				}
			}
			
			self.lightpistol_animset = 0;
			bp_skipme = 0;
			////END NORMAL////
		  }
		}
		
	}
	else if (req == WR_PRECACHE)
	{
		precache_model ("models/uziflash.md3");
		precache_model ("models/weapons/g_lightpistol.md3");
		precache_model ("models/weapons/v_lightpistol.md3");
		precache_model ("models/weapons/g_lightpistol_suppressed.md3");
		precache_model ("models/weapons/v_lightpistol_suppressed.md3");
		precache_model ("models/weapons/h_lightpistol.iqm");
		precache_model ("models/weapons/h_lightpistol_suppressed.iqm");
		
		precache_model ("models/weapons/v_lightpistol_empty.md3");
		precache_model ("models/weapons/v_lightpistol_suppressed_empty.md3");
		precache_model ("models/weapons/h_lightpistol_empty.iqm");
		precache_model ("models/weapons/h_lightpistol_suppressed_empty.iqm");
		precache_sound ("misc/itempickup.wav");
		precache_sound ("weapons/ffsl_wppq_fire.wav");
		precache_sound ("weapons/uzi_fire_doublesuppressed.wav");
		//precache_sound ("weapons/reload.wav"); // until weapons have individual reload sounds, precache the reload sound somewhere else
	}
	else if (req == WR_SETUP)
	{
		self.weaponaltaim_use = 1;
		self.weaponaltaim = '40 -0.35 -0.9';
			
		weapon_setup(WEP_LIGHTPISTOL);
		self.current_ammo = ammo_nails;
		
		if (!autocvar_g_balance_lightpistol_suppressornotincluded
		|| self.inv_misc_suppressor9mm)
		{
			if (self.lightpistol_suppressed) {
				if (self.lightpistol_animempty)
					self.weaponname = "lightpistol_suppressed_empty";
				else
					self.weaponname = "lightpistol_suppressed";
			} else if (!self.lightpistol_suppressed) {
				if (self.lightpistol_animempty)
					self.weaponname = "lightpistol_empty";
				else
					self.weaponname = "lightpistol";
			}
		} else {
			self.lightpistol_suppressed = 0;
			
			if (self.lightpistol_animempty)
				self.weaponname = "lightpistol_empty";
			else
				self.weaponname = "lightpistol";
		}
		
		
		  if (self.lightpistol_jammed && self.lightpistol_unjam)
		  {
		  	if (time >= self.lightpistol_unjam_time) {
				self.lightpistol_jammed = 0;
				self.lightpistol_unjam = 0;
				if ((clienttype(self) == CLIENTTYPE_REAL)) {
					sprint(self, "You cleared the malfunction\n");
				}
			}
		  }
	}
	else if (req == WR_CHECKAMMO1)
	{
		ammo_amount = self.ammo_nails >= autocvar_g_balance_lightpistol_primary_ammo;
		ammo_amount += self.(weapon_load[WEP_LIGHTPISTOL]) >= autocvar_g_balance_lightpistol_primary_ammo;
		return ammo_amount;
	}
	else if (req == WR_CHECKAMMO2)
	{
		// melee attack is always available
		return TRUE;
	}
	else if (req == WR_RELOAD)
	{
		if (self.lightpistol_jammed) {
			weapon_thinkf(WFRAME_RELOAD, autocvar_g_balance_lightpistol_clearjam_time, w_ready);
			if (self.lightpistol_unjam != 1) {
				self.lightpistol_unjam = 1;
				self.lightpistol_unjam_time = time + autocvar_g_balance_lightpistol_clearjam_time;
				self.lightpistol_click = 0;
				if ((clienttype(self) == CLIENTTYPE_REAL)) {
					sprint(self, "You work on the weapon\n");
				}
			}
		} else {
			W_Reload(autocvar_g_balance_lightpistol_primary_ammo, autocvar_g_balance_lightpistol_reload_ammo, autocvar_g_balance_lightpistol_reload_time, "weapons/reload.wav");
			
			if (self.BUTTON_USE) {
			  if (!autocvar_g_balance_lightpistol_suppressornotincluded
			  || self.inv_misc_suppressor9mm)
			  {
				if (self.lightpistol_suppressed) {
					self.lightpistol_suppressed = 0;
					if (self.lightpistol_animempty)
						self.weaponname = "lightpistol_empty";
					else
						self.weaponname = "lightpistol";
				} else if (!self.lightpistol_suppressed) {
					self.lightpistol_suppressed = 1;
					if (self.lightpistol_animempty)
						self.weaponname = "lightpistol_suppressed_empty";
					else
						self.weaponname = "lightpistol_suppressed";
				}
			  }
			}
		}
	}
	return TRUE;
}
#endif
#ifdef CSQC
.float prevric;
float w_lightpistol(float req)
{
	if(req == WR_IMPACTEFFECT)
	{
		vector org2;
		org2 = w_org + w_backoff * 2;
		pointparticles(particleeffectnum("machinegun_impact"), org2, w_backoff * 1000, 1);
		if(!w_issilent && time - self.prevric > 0.25)
		{
			if(w_random < 0.0165)
				sound(self, CH_SHOTS, "weapons/ric1.wav", VOL_BASE, ATTN_NORM);
			else if(w_random < 0.033)
				sound(self, CH_SHOTS, "weapons/ric2.wav", VOL_BASE, ATTN_NORM);
			else if(w_random < 0.05)
				sound(self, CH_SHOTS, "weapons/ric3.wav", VOL_BASE, ATTN_NORM);
			self.prevric = time;
		}
	}
	else if(req == WR_PRECACHE)
	{
		precache_sound("weapons/ric1.wav");
		precache_sound("weapons/ric2.wav");
		precache_sound("weapons/ric3.wav");
	}
	else if (req == WR_SUICIDEMESSAGE)
		w_deathtypestring = _("%s is now thinking with portals");
	else if (req == WR_KILLMESSAGE)
	{
		if(w_deathtype & HITTYPE_CORESHOT)
			w_deathtypestring = _("%1$s chest cavity was gifted some caps from %2$s gun");
		else if(w_deathtype & HITTYPE_HEADSHOT)
			w_deathtypestring = _("%1$s died when %2$s cracked their dome with a 9mm");
		else
			w_deathtypestring = _("%s was put away by %s's 9mm");
	}
	return TRUE;
}
#endif
#endif
