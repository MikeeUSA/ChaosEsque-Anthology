#ifdef REGISTER_WEAPON
REGISTER_WEAPON(M16VN, w_m16vn, IT_NAILS, 3, WEP_FLAG_NORMAL | WEP_FLAG_RELOADABLE | WEP_TYPE_HITSCAN, BOT_PICKUP_RATING_LOW, "m16vn", "m16vn", _("M16-Vn"))
#else
#ifdef SVQC

.float m16vn_release;
.float m16vn_jammed;
.float m16vn_unjam;
.float m16vn_unjam_time;
.float m16vn_click;
.float m16vn_animempty;
.float m16vn_animset;
.float m16vn_nextanim;
.float m16vn_nextbot;
.float m16vn_setting; //0 full auto //1 semi
.float m16vn_overheat_cnt;
.float m16vn_overheated_until;

void W_M16Vn_Attack (void)
{
	float	ammoamount;
	float	d;
	float	f;
	float	spread;
	float	bulletspeed;
	float	bulletconstant;
	entity flash;

	ammoamount = autocvar_g_balance_m16vn_primary_ammo;
	d = autocvar_g_balance_m16vn_primary_damage;
	f = autocvar_g_balance_m16vn_primary_force;
	//spread = autocvar_g_balance_m16vn_primary_spread;
	spread = bound(autocvar_g_balance_m16vn_spread_min, autocvar_g_balance_m16vn_spread_min + (autocvar_g_balance_m16vn_spread_add * self.misc_bulletcounter), autocvar_g_balance_m16vn_spread_max);

	//print(ftos(spread), " spread\n");

	bulletspeed = autocvar_g_balance_m16vn_primary_speed;
	bulletconstant = autocvar_g_balance_m16vn_primary_bulletconstant;

	W_DecreaseAmmo(ammo_nails, ammoamount, autocvar_g_balance_m16vn_reload_ammo);
	
	if (autocvar_g_balance_m16vn_jamrate &&
 	(autocvar_g_balance_m16vn_jamrate > random() ))
 	{
		if ((clienttype(self) == CLIENTTYPE_REAL)) {
			sprint(self, "Your assault rifle jammed\n");
		}
		self.m16vn_jammed = 1;
	} else { //Start Not Jammed


		W_SetupShot (self, autocvar_g_antilag_bullets && bulletspeed >= autocvar_g_antilag_bullets, 5, "weapons/ffsl_ar15_fire.wav", CH_WEAPON_A, d);
	
	W_MaybeShootFromEyeForAltAim(self); //shoot from the eye maybe if in alt aim so that shooting through windows works correctly when aiming
	
	fireBallisticBullet(w_shotorg, w_shotdir, spread, bulletspeed, 5, d, autocvar_g_balance_m16vn_primary_headshotaddeddamage, f, WEP_M16VN, PROJECTILE_BULLETRIFLE22, 1, bulletconstant, autocvar_g_balance_m16vn_primary_coreshotaddeddamage);
	endFireBallisticBullet();

	if (!g_norecoil)
	{
		if (self.misc_bulletcounter) {
		//If we've been shooting full auto, shake things around
		self.punchangle_x = random () - 0.35;
		self.punchangle_y = random () - 0.35;
		}
	}
	self.misc_bulletcounter = self.misc_bulletcounter + 1;

		pointparticles(particleeffectnum("uzi_muzzleflash"), w_shotorg, w_shotdir * 1000, autocvar_g_balance_m16vn_primary_ammo);
	
		PlayerGunRise(self, autocvar_g_balance_m16vn_gunrise, random() * autocvar_g_balance_m16vn_gunrun);

		// muzzle flash for 1st person view
		flash = spawn();
		setmodel(flash, "models/uziflash.md3"); // precision set below
		flash.think = SUB_Remove;
		flash.nextthink = time + 0.06;
		flash.effects = EF_ADDITIVE | EF_FULLBRIGHT | EF_LOWPRECISION;
		W_AttachToShotorg(flash, '12 0 0');
	
	
	if (autocvar_g_casings >= 2)
		SpawnCasing (((random () * 50 + 50) * v_right) - (v_forward * (random () * 30 + 30)) - ((random () * -120 - 140) * v_up), 2, vectoangles(v_forward),'0 250 0', 100, 103, self);

	
	if not (autocvar_g_nosmallarmsoverheating) {
	    	//Overheating//
			//print(ftos(self.m16vn_overheat_cnt),"_Firing\n");
		
	    	self.m16vn_overheat_cnt = self.m16vn_overheat_cnt + autocvar_g_balance_m16vn_heatconstant;
         
	    	if (self.m16vn_overheat_cnt > random() )
 	    	{
			if ((clienttype(self) == CLIENTTYPE_REAL)) {
				sprint(self, "Your m16vn overheated\n");
			}
				
			self.m16vn_overheated_until = time + (autocvar_g_balance_m16vn_cooldownwait * (self.m16vn_overheat_cnt + 1));
	    	}
	    	//Overheating//
	}
	
	
	} //End Not Jammed
}


float W_M16Vn_NeedManualReload() {
	if  (autocvar_g_manualreload && (autocvar_g_balance_m16vn_reload_ammo && self.clip_load < autocvar_g_balance_m16vn_primary_ammo))
	{
		if (!self.m16vn_click && (self.BUTTON_ATCK)) {
			spamsound (self, CH_SHOTS, "weapons/dryfire.wav", VOL_BASE * 0.25, ATTN_NORM);
			self.m16vn_click = 1;
		}
		else if not(self.BUTTON_ATCK)
		{
			self.m16vn_click = 0;
		}
		return 1;
	} else {
		return 0;
	}
	
}


void spawnfunc_weapon_m16vn (void)
{
	weapon_defaultspawnfunc(WEP_M16VN);
}


.float m16vn_primarytime;

float w_m16vn(float req)
{

	if (!autocvar_g_nosmallarmsoverheating) {
	//Overheating and cooldown stuff://
		if (!self.BUTTON_ATCK && !self.BUTTON_ATCK2) {
				//Normally, secondary would be irrelevant here
				//(Being a selector switch)
				//However m16s are tempermental
				//when not handled exactly correctly
				//print(ftos(self.m16vn_overheat_cnt),"_Cooldown\n");
			
			if (self.m16vn_overheat_cnt < autocvar_g_balance_m16vn_heatmin) {
				self.m16vn_overheat_cnt = autocvar_g_balance_m16vn_heatmin;
			} else {
				self.m16vn_overheat_cnt = self.m16vn_overheat_cnt - (autocvar_g_balance_m16vn_cooldownrate * sys_frametime);
			}
		}
	//Overheating and cooldown stuff Done//
	}
	
	
	float ammo_amount;
	if (req == WR_AIM) {
		if (W_M16Vn_NeedManualReload()) { weapon_action(self.weapon, WR_RELOAD); } // bot reload if automatic reload off

		if (self.m16vn_jammed) {
			 weapon_action(self.weapon, WR_RELOAD);
		}
		
		self.BUTTON_ATCK=FALSE;
		self.BUTTON_ATCK2=FALSE;
		
		if (self.m16vn_setting != 1) {
			self.BUTTON_ATCK2 = 1;
			//Flip to full auto if bot
		} else {
		
			if (self.m16vn_release) {
				if(autocvar_g_antilag_bullets)
					self.BUTTON_ATCK = bot_aim(1000000, 0, 0.001, FALSE);
				else
					self.BUTTON_ATCK = bot_aim(autocvar_g_balance_m16vn_primary_speed, 0, 0.001, FALSE);
			}
		}
		
	}
	else if (req == WR_THINK && !W_M16Vn_NeedManualReload())
	{
		if(autocvar_g_balance_m16vn_reload_ammo && self.clip_load < autocvar_g_balance_m16vn_primary_ammo) // forced reload
		{
			// don't force reload an empty m16vn if its melee attack is active
			if not(autocvar_g_balance_m16vn_secondary && self.ammo_nails < autocvar_g_balance_m16vn_primary_ammo)
				weapon_action(self.weapon, WR_RELOAD);
				//self.m16vn_release = 0;
		}
		else
		{
		  if (self.m16vn_jammed && self.m16vn_unjam)
		  {
		  	if (time >= self.m16vn_unjam_time) {
				self.m16vn_jammed = 0;
				self.m16vn_unjam = 0;
				if ((clienttype(self) == CLIENTTYPE_REAL)) {
					sprint(self, "You cleared the jam\n");
				}
			}
		  } else {
			float bp_skipme;
			if (self.BUTTON_ATCK)
			if (self.m16vn_release)
			if (self.m16vn_jammed != 1)
			if ((time > self.m16vn_overheated_until) || autocvar_g_nosmallarmsoverheating)
			if (weapon_prepareattack_check(0, 0))
			{
			  if (self.m16vn_setting == 1) {
			  	//Fullauto
				if(clienttype(self) == CLIENTTYPE_BOT) {
					//Nope
					//print("I bot", ftos(self.m16vn_nextbot)," \n");
				} else {
					self.m16vn_nextbot = 0;
				}
				
				if (time >= self.m16vn_nextbot) {
					W_M16Vn_Attack();
					ATTACK_FINISHED(self) = time + autocvar_g_balance_m16vn_primary_refire * W_WeaponRateFactor();
					weapon_thinkf(WFRAME_FIRE1, autocvar_g_balance_m16vn_primary_animtime, w_ready);
					self.m16vn_release = 0;
					self.m16vn_animempty = 1;
					self.m16vn_animset = 1;
					self.m16vn_nextanim = time + autocvar_g_balance_m16vn_primary_rfanim;
					bp_skipme = 1;
					//self.m16vn_nextbot = time + (autocvar_g_balance_m16vn_primary_refire * 50 * random());
					self.m16vn_nextbot = time + 0.1 + (0.4 * random());
					
					//Fullauto:
					self.m16vn_release = 1;
					self.m16vn_nextbot = time;
				}
			   } else {
				//Semiauto
				if(clienttype(self) == CLIENTTYPE_BOT) {
					//Nope
					//print("I bot", ftos(self.m16vn_nextbot)," \n");
				} else {
					self.m16vn_nextbot = 0;
				}
				
				if (time >= self.m16vn_nextbot) {
					W_M16Vn_Attack();
					ATTACK_FINISHED(self) = time + autocvar_g_balance_m16vn_secondary_refire * W_WeaponRateFactor();
					weapon_thinkf(WFRAME_FIRE1, autocvar_g_balance_m16vn_secondary_animtime, w_ready);
					self.m16vn_release = 0;
					self.m16vn_animempty = 1;
					self.m16vn_animset = 1;
					self.m16vn_nextanim = time + autocvar_g_balance_m16vn_secondary_rfanim;
					bp_skipme = 1;
					//self.m16vn_nextbot = time + (autocvar_g_balance_m16vn_secondary_refire * 50 * random());
					self.m16vn_nextbot = time + 0.1 + (0.4 * random());
				}
			   }
			}
			
			if (self.BUTTON_ATCK2)
			if (self.m16vn_release)
			if (self.m16vn_jammed != 1)
			if ((time > self.m16vn_overheated_until) || autocvar_g_nosmallarmsoverheating)
			if (weapon_prepareattack_check(0, 0))
			{
				if(self.m16vn_setting) {
					self.m16vn_setting = 0;
				} else {
					self.m16vn_setting = 1;
				}
				spamsound (self, CH_SHOTS, "weapons/dryfire.wav", VOL_BASE * 0.20, ATTN_NORM);
				self.m16vn_release = 0;
			}
			
			if not(bp_skipme) {
				if (self.m16vn_jammed != 1)
				if (time >= self.m16vn_nextanim)
				if (self.m16vn_animempty)
				{
					self.m16vn_animempty = 0;
					self.m16vn_animset = 1;
				}
			}
			
			if not(self.BUTTON_ATCK || self.BUTTON_ATCK2) {
				self.m16vn_release = 1;
			}
			
			if not(self.BUTTON_ATCK) {
				//Not full auto, reset bullet counter
				self.misc_bulletcounter = 0;
			}
				
			//if (self.m16vn_animset)
			//if (self.m16vn_jammed != 1) {
			//	
			//		if (self.m16vn_animempty)
			//			self.weaponname = "m16vn_empty";
			//		else
			//			self.weaponname = "m16vn";
			//}
			
			self.m16vn_animset = 0;
			bp_skipme = 0;
			////END NORMAL////
		  }
		}
		
	}
	else if (req == WR_PRECACHE)
	{
		precache_model ("models/uziflash.md3");
		precache_model ("models/weapons/g_m16vn.md3");
		precache_model ("models/weapons/v_m16vn.md3");
		precache_model ("models/weapons/h_m16vn.iqm");
		
		//precache_model ("models/weapons/v_m16vn_empty.md3");
		//precache_model ("models/weapons/h_m16vn_empty.iqm");
		precache_sound ("misc/itempickup.wav");
		precache_sound ("weapons/ffsl_ar15_fire.wav");
		//precache_sound ("weapons/reload.wav"); // until weapons have individual reload sounds, precache the reload sound somewhere else
	}
	else if (req == WR_SETUP)
	{
		self.m16vn_overheat_cnt = autocvar_g_balance_m16vn_heatmin;
		self.weaponaltaim_use = 1;
		self.weaponaltaim = '43 0.02 -2.8';
		weapon_setup(WEP_M16VN);
		self.current_ammo = ammo_nails;
		
		
			//if (self.m16vn_animempty)
			//	self.weaponname = "m16vn_empty";
			//else
			//	self.weaponname = "m16vn";
				
		  
		  //When a jam occurs when there is no ammo left, then one cannot reach the routine to unjam
		  //Thus is a "malfunction" and takes more to fix (switching weapon)
		  //With the M16 we have set it so that if a malfunction (rather than a jam) occurs it is hard to clear
		  if (self.m16vn_jammed && self.m16vn_unjam)
		  {
		  	if (time >= self.m16vn_unjam_time) {
			if (random() > 0.95) {
				self.m16vn_jammed = 0;
				self.m16vn_unjam = 0;
				if ((clienttype(self) == CLIENTTYPE_REAL)) {
					sprint(self, "You cleared the malfunction\n");
				}
			}
			}
		  }
	}
	else if (req == WR_CHECKAMMO1)
	{
		ammo_amount = self.ammo_nails >= autocvar_g_balance_m16vn_primary_ammo;
		ammo_amount += self.(weapon_load[WEP_M16VN]) >= autocvar_g_balance_m16vn_primary_ammo;
		return ammo_amount;
	}
	else if (req == WR_CHECKAMMO2)
	{
		// melee attack is always available
		return TRUE;
	}
	else if (req == WR_RELOAD)
	{
		if (self.m16vn_jammed) {
			weapon_thinkf(WFRAME_RELOAD, autocvar_g_balance_m16vn_clearjam_time, w_ready);
			if (self.m16vn_unjam != 1) {
				self.m16vn_unjam = 1;
				self.m16vn_unjam_time = time + autocvar_g_balance_m16vn_clearjam_time;
				self.m16vn_click = 0;
				if ((clienttype(self) == CLIENTTYPE_REAL)) {
					sprint(self, "You work on the weapon\n");
				}
			}
		} else {
			W_Reload(autocvar_g_balance_m16vn_primary_ammo, autocvar_g_balance_m16vn_reload_ammo, autocvar_g_balance_m16vn_reload_time, "weapons/reload.wav");
		}
	}
	return TRUE;
}
#endif
#ifdef CSQC
.float prevric;
float w_m16vn(float req)
{
	if(req == WR_IMPACTEFFECT)
	{
		vector org2;
		org2 = w_org + w_backoff * 2;
		pointparticles(particleeffectnum("machinegun_impact"), org2, w_backoff * 1000, 1);
		if(!w_issilent && time - self.prevric > 0.25)
		{
			if(w_random < 0.0165)
				sound(self, CH_SHOTS, "weapons/ric1.wav", VOL_BASE, ATTN_NORM);
			else if(w_random < 0.033)
				sound(self, CH_SHOTS, "weapons/ric2.wav", VOL_BASE, ATTN_NORM);
			else if(w_random < 0.05)
				sound(self, CH_SHOTS, "weapons/ric3.wav", VOL_BASE, ATTN_NORM);
			self.prevric = time;
		}
	}
	else if(req == WR_PRECACHE)
	{
		precache_sound("weapons/ric1.wav");
		precache_sound("weapons/ric2.wav");
		precache_sound("weapons/ric3.wav");
	}
	else if (req == WR_SUICIDEMESSAGE)
		w_deathtypestring = _("%s is now thinking with portals");
	else if (req == WR_KILLMESSAGE)
	{
		if(w_deathtype & HITTYPE_CORESHOT)
			w_deathtypestring = _("%s had .223 sized holes in the chest thanks to %s");
		else if(w_deathtype & HITTYPE_HEADSHOT)
			w_deathtypestring = _("%s was designated shot by %s's M16");
		else
			w_deathtypestring = _("%s shot dead by %s with an M16");
	}
	return TRUE;
}
#endif
#endif
