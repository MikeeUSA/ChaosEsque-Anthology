#ifdef REGISTER_WEAPON
REGISTER_WEAPON(BAR1918, w_bar1918, IT_NAILS, 6, WEP_FLAG_NORMAL | WEP_FLAG_RELOADABLE | WEP_TYPE_HITSCAN, BOT_PICKUP_RATING_LOW, "bar1918", "bar1918", _("BAR-1918"))
#else
#ifdef SVQC

.float bar1918_release;
.float bar1918_jammed;
.float bar1918_unjam;
.float bar1918_unjam_time;
.float bar1918_click;
.float bar1918_animempty;
.float bar1918_animset;
.float bar1918_nextanim;
.float bar1918_nextbot;
.float bar1918_setting; //0 full auto //1 semi
.float bar1918_overheat_cnt;
.float bar1918_overheated_until;


void W_Bar1918_Attack (void)
{
	float	ammoamount;
	float	d;
	float	f;
	float	spread;
	float	bulletspeed;
	float	bulletconstant;
	entity flash;

	ammoamount = autocvar_g_balance_bar1918_primary_ammo;
	d = autocvar_g_balance_bar1918_primary_damage;
	f = autocvar_g_balance_bar1918_primary_force;
	//spread = autocvar_g_balance_bar1918_primary_spread;
	spread = bound(autocvar_g_balance_bar1918_spread_min, autocvar_g_balance_bar1918_spread_min + (autocvar_g_balance_bar1918_spread_add * self.misc_bulletcounter), autocvar_g_balance_bar1918_spread_max);

	//print(ftos(spread), " spread\n");

	bulletspeed = autocvar_g_balance_bar1918_primary_speed;
	bulletconstant = autocvar_g_balance_bar1918_primary_bulletconstant;

	W_DecreaseAmmo(ammo_nails, ammoamount, autocvar_g_balance_bar1918_reload_ammo);
	
	if (autocvar_g_balance_bar1918_jamrate &&
 	(autocvar_g_balance_bar1918_jamrate > random() ))
 	{
		if ((clienttype(self) == CLIENTTYPE_REAL)) {
			sprint(self, "Your automatic rifle jammed\n");
		}
		self.bar1918_jammed = 1;
	} else { //Start Not Jammed


		W_SetupShot (self, autocvar_g_antilag_bullets && bulletspeed >= autocvar_g_antilag_bullets, 5, "weapons/uzi_fire_bass.ogg", CH_WEAPON_A, d);
	
	W_MaybeShootFromEyeForAltAim(self); //shoot from the eye maybe if in alt aim so that shooting through windows works correctly when aiming
	
	fireBallisticBullet(w_shotorg, w_shotdir, spread, bulletspeed, 5, d, autocvar_g_balance_bar1918_primary_headshotaddeddamage, f, WEP_BAR1918, PROJECTILE_BULLETRIFLE9MM, 1, bulletconstant, autocvar_g_balance_bar1918_primary_coreshotaddeddamage);
	endFireBallisticBullet();

	if (!g_norecoil)
	{
		if (self.misc_bulletcounter) {
		//If we've been shooting full auto, shake things around
		self.punchangle_x = random () - 0.5;
		self.punchangle_y = random () - 0.5;
		}
	}
	self.misc_bulletcounter = self.misc_bulletcounter + 1;

		pointparticles(particleeffectnum("uzi_muzzleflash"), w_shotorg, w_shotdir * 1000, autocvar_g_balance_bar1918_primary_ammo);
	
		PlayerGunRise(self, autocvar_g_balance_bar1918_gunrise, random() * autocvar_g_balance_bar1918_gunrun);

		// muzzle flash for 1st person view
		flash = spawn();
		setmodel(flash, "models/uziflash.md3"); // precision set below
		flash.think = SUB_Remove;
		flash.nextthink = time + 0.06;
		flash.effects = EF_ADDITIVE | EF_FULLBRIGHT | EF_LOWPRECISION;
		W_AttachToShotorg(flash, '32 0 0');
	
	
	if (autocvar_g_casings >= 2)
		SpawnCasing_Offset (((random () * 50 + 150) * v_right) - (v_forward * (random () * 25 + 25)) - ((random () * -100 - 220) * v_up), 2, vectoangles(v_forward),'0 250 0', 100, 105, self, '8 0 0');

	
	if not (autocvar_g_nosmallarmsoverheating) {
	    	//Overheating//
			//print(ftos(self.bar1918_overheat_cnt),"_Firing\n");
		
	    	self.bar1918_overheat_cnt = self.bar1918_overheat_cnt + autocvar_g_balance_bar1918_heatconstant;
         
	    	if (self.bar1918_overheat_cnt > random() )
 	    	{
			if ((clienttype(self) == CLIENTTYPE_REAL)) {
				sprint(self, "Your bar1918 overheated\n");
			}
				
			self.bar1918_overheated_until = time + (autocvar_g_balance_bar1918_cooldownwait * (self.bar1918_overheat_cnt + 1));
	    	}
	    	//Overheating//
	}
	
	
	
	} //End Not Jammed
}


float W_Bar1918_NeedManualReload() {
	if  (autocvar_g_manualreload && (autocvar_g_balance_bar1918_reload_ammo && self.clip_load < autocvar_g_balance_bar1918_primary_ammo))
	{
		if (!self.bar1918_click && (self.BUTTON_ATCK)) {
			spamsound (self, CH_SHOTS, "weapons/dryfire.wav", VOL_BASE * 0.25, ATTN_NORM);
			self.bar1918_click = 1;
		}
		else if not(self.BUTTON_ATCK)
		{
			self.bar1918_click = 0;
		}
		return 1;
	} else {
		return 0;
	}
	
}


void spawnfunc_weapon_bar1918 (void)
{
	weapon_defaultspawnfunc(WEP_BAR1918);
}


.float bar1918_primarytime;

float w_bar1918(float req)
{

	if (!autocvar_g_nosmallarmsoverheating) {
	//Overheating and cooldown stuff://
		if (!self.BUTTON_ATCK) {
				//Secondary is selector switch, so irrelevant
				//print(ftos(self.bar1918_overheat_cnt),"_Cooldown\n");
			
			if (self.bar1918_overheat_cnt < autocvar_g_balance_bar1918_heatmin) {
				self.bar1918_overheat_cnt = autocvar_g_balance_bar1918_heatmin;
			} else {
				self.bar1918_overheat_cnt = self.bar1918_overheat_cnt - (autocvar_g_balance_bar1918_cooldownrate * sys_frametime);
			}
		}
	//Overheating and cooldown stuff Done//
	}
	
	
	float ammo_amount;
	if (req == WR_AIM) {
		if (W_Bar1918_NeedManualReload()) { weapon_action(self.weapon, WR_RELOAD); } // bot reload if automatic reload off

		if (self.bar1918_jammed) {
			 weapon_action(self.weapon, WR_RELOAD);
		}
		
		self.BUTTON_ATCK=FALSE;
		self.BUTTON_ATCK2=FALSE;

		if (self.bar1918_release) {
			if(autocvar_g_antilag_bullets)
				self.BUTTON_ATCK = bot_aim(1000000, 0, 0.001, FALSE);
			else
				self.BUTTON_ATCK = bot_aim(autocvar_g_balance_bar1918_primary_speed, 0, 0.001, FALSE);
		}
		
	}
	else if (req == WR_THINK && !W_Bar1918_NeedManualReload())
	{
		if(autocvar_g_balance_bar1918_reload_ammo && self.clip_load < autocvar_g_balance_bar1918_primary_ammo) // forced reload
		{
			// don't force reload an empty bar1918 if its melee attack is active
			if not(autocvar_g_balance_bar1918_secondary && self.ammo_nails < autocvar_g_balance_bar1918_primary_ammo)
				weapon_action(self.weapon, WR_RELOAD);
				//self.bar1918_release = 0;
		}
		else
		{
		  if (self.bar1918_jammed && self.bar1918_unjam)
		  {
		  	if (time >= self.bar1918_unjam_time) {
				self.bar1918_jammed = 0;
				self.bar1918_unjam = 0;
				if ((clienttype(self) == CLIENTTYPE_REAL)) {
					sprint(self, "You cleared the jam\n");
				}
			}
		  } else {
			float bp_skipme;
			if (self.BUTTON_ATCK)
			if (self.bar1918_release)
			if (self.bar1918_jammed != 1)
			if ((time > self.bar1918_overheated_until) || autocvar_g_nosmallarmsoverheating)
			if (weapon_prepareattack_check(0, 0))
			{
			  if (self.bar1918_setting != 1) {
			  	//Fullauto
				if(clienttype(self) == CLIENTTYPE_BOT) {
					//Nope
					//print("I bot", ftos(self.bar1918_nextbot)," \n");
				} else {
					self.bar1918_nextbot = 0;
				}
				
				if (time >= self.bar1918_nextbot) {
					W_Bar1918_Attack();
					ATTACK_FINISHED(self) = time + autocvar_g_balance_bar1918_primary_refire * W_WeaponRateFactor();
					weapon_thinkf(WFRAME_FIRE1, autocvar_g_balance_bar1918_primary_animtime, w_ready);
					self.bar1918_release = 0;
					self.bar1918_animempty = 1;
					self.bar1918_animset = 1;
					self.bar1918_nextanim = time + autocvar_g_balance_bar1918_primary_rfanim;
					bp_skipme = 1;
					//self.bar1918_nextbot = time + (autocvar_g_balance_bar1918_primary_refire * 50 * random());
					self.bar1918_nextbot = time + 0.1 + (0.4 * random());
					
					//Fullauto:
					self.bar1918_release = 1;
					self.bar1918_nextbot = time;
				}
			   } else {
				//Semiauto
				if(clienttype(self) == CLIENTTYPE_BOT) {
					//Nope
					//print("I bot", ftos(self.bar1918_nextbot)," \n");
				} else {
					self.bar1918_nextbot = 0;
				}
				
				if (time >= self.bar1918_nextbot) {
					W_Bar1918_Attack();
					ATTACK_FINISHED(self) = time + autocvar_g_balance_bar1918_secondary_refire * W_WeaponRateFactor();
					weapon_thinkf(WFRAME_FIRE1, autocvar_g_balance_bar1918_secondary_animtime, w_ready);
					self.bar1918_release = 0;
					self.bar1918_animempty = 1;
					self.bar1918_animset = 1;
					self.bar1918_nextanim = time + autocvar_g_balance_bar1918_secondary_rfanim;
					bp_skipme = 1;
					//self.bar1918_nextbot = time + (autocvar_g_balance_bar1918_secondary_refire * 50 * random());
					self.bar1918_nextbot = time + 0.1 + (0.4 * random());
				}
			   }
			}
			
			if (self.BUTTON_ATCK2)
			if (self.bar1918_release)
			if (self.bar1918_jammed != 1)
			if ((time > self.bar1918_overheated_until) || autocvar_g_nosmallarmsoverheating)
			if (weapon_prepareattack_check(0, 0))
			{
				if(self.bar1918_setting) {
					self.bar1918_setting = 0;
				} else {
					self.bar1918_setting = 1;
				}
				spamsound (self, CH_SHOTS, "weapons/dryfire.wav", VOL_BASE * 0.20, ATTN_NORM);
				self.bar1918_release = 0;
			}
			
			if not(bp_skipme) {
				if (self.bar1918_jammed != 1)
				if (time >= self.bar1918_nextanim)
				if (self.bar1918_animempty)
				{
					self.bar1918_animempty = 0;
					self.bar1918_animset = 1;
				}
			}
			
			if not(self.BUTTON_ATCK || self.BUTTON_ATCK2) {
				self.bar1918_release = 1;
			}
			
			if not(self.BUTTON_ATCK) {
				//Not full auto, reset bullet counter
				self.misc_bulletcounter = 0;
			}
				
			if (self.bar1918_animset)
			if (self.bar1918_jammed != 1) {
				
					//Open bolt: reversed
					if (self.bar1918_animempty)
						self.weaponname = "bar1918";
					else
						self.weaponname = "bar1918_empty";
			}
			
			self.bar1918_animset = 0;
			bp_skipme = 0;
			////END NORMAL////
		  }
		}
		
	}
	else if (req == WR_PRECACHE)
	{
		precache_model ("models/uziflash.md3");
		precache_model ("models/weapons/g_bar1918.md3");
		precache_model ("models/weapons/v_bar1918.md3");
		precache_model ("models/weapons/h_bar1918.iqm");
		
		precache_model ("models/weapons/v_bar1918_empty.md3");
		precache_model ("models/weapons/h_bar1918_empty.iqm");
		precache_sound ("misc/itempickup.wav");
		precache_sound ("weapons/uzi_fire_bass.ogg");
		//precache_sound ("weapons/reload.wav"); // until weapons have individual reload sounds, precache the reload sound somewhere else
	}
	else if (req == WR_SETUP)
	{
		self.bar1918_overheat_cnt = autocvar_g_balance_bar1918_heatmin;
		self.weaponaltaim_use = 1;
		//self.weaponaltaim = '43 0.02 -2.8';
		self.weaponaltaim = '43 -0.23 -3.35';
		weapon_setup(WEP_BAR1918);
		self.current_ammo = ammo_nails;
		
			//Open bolt: reversed
			if (self.bar1918_animempty)
				self.weaponname = "bar1918";
			else
				self.weaponname = "bar1918_empty";
				
	
	
		
		  if (self.bar1918_jammed && self.bar1918_unjam)
		  {
		  	if (time >= self.bar1918_unjam_time) {
				self.bar1918_jammed = 0;
				self.bar1918_unjam = 0;
				if ((clienttype(self) == CLIENTTYPE_REAL)) {
					sprint(self, "You cleared the malfunction\n");
				}
			}
		  }
	}
	else if (req == WR_CHECKAMMO1)
	{
		ammo_amount = self.ammo_nails >= autocvar_g_balance_bar1918_primary_ammo;
		ammo_amount += self.(weapon_load[WEP_BAR1918]) >= autocvar_g_balance_bar1918_primary_ammo;
		return ammo_amount;
	}
	else if (req == WR_CHECKAMMO2)
	{
		// melee attack is always available
		return TRUE;
	}
	else if (req == WR_RELOAD)
	{
		if (self.bar1918_jammed) {
			weapon_thinkf(WFRAME_RELOAD, autocvar_g_balance_bar1918_clearjam_time, w_ready);
			if (self.bar1918_unjam != 1) {
				self.bar1918_unjam = 1;
				self.bar1918_unjam_time = time + autocvar_g_balance_bar1918_clearjam_time;
				self.bar1918_click = 0;
				if ((clienttype(self) == CLIENTTYPE_REAL)) {
					sprint(self, "You work on the weapon\n");
				}
			}
		} else {
			W_Reload(autocvar_g_balance_bar1918_primary_ammo, autocvar_g_balance_bar1918_reload_ammo, autocvar_g_balance_bar1918_reload_time, "weapons/reload.wav");
		}
	}
	return TRUE;
}
#endif
#ifdef CSQC
.float prevric;
float w_bar1918(float req)
{
	if(req == WR_IMPACTEFFECT)
	{
		vector org2;
		org2 = w_org + w_backoff * 2;
		pointparticles(particleeffectnum("machinegun_impact"), org2, w_backoff * 1000, 1);
		if(!w_issilent && time - self.prevric > 0.25)
		{
			if(w_random < 0.0165)
				sound(self, CH_SHOTS, "weapons/ric1.wav", VOL_BASE, ATTN_NORM);
			else if(w_random < 0.033)
				sound(self, CH_SHOTS, "weapons/ric2.wav", VOL_BASE, ATTN_NORM);
			else if(w_random < 0.05)
				sound(self, CH_SHOTS, "weapons/ric3.wav", VOL_BASE, ATTN_NORM);
			self.prevric = time;
		}
	}
	else if(req == WR_PRECACHE)
	{
		precache_sound("weapons/ric1.wav");
		precache_sound("weapons/ric2.wav");
		precache_sound("weapons/ric3.wav");
	}
	else if (req == WR_SUICIDEMESSAGE)
		w_deathtypestring = _("%s is now thinking with portals");
	else if (req == WR_KILLMESSAGE)
	{
		if(w_deathtype & HITTYPE_CORESHOT)
			w_deathtypestring = _("%1$s tried to breathe but %2$s's Automatic Rifle gave silence");
		else if(w_deathtype & HITTYPE_HEADSHOT)
			w_deathtypestring = _("%1$s died, %2$s smashed them in the head with a bullet from a BAR");
		else
			w_deathtypestring = _("%s was killed by %s's Browning Automatic Rifle");
	}
	return TRUE;
}
#endif
#endif
